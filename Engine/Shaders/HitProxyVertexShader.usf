/*=============================================================================
	HitProxyVertexShader.hlsl: Vertex shader for rendering hit proxies.
	Copyright 1998-2011 Epic Games, Inc. All Rights Reserved.
=============================================================================*/

#include "Common.usf"
#include "Material.usf"
#include "VertexFactory.usf"

struct FHitProxyVSToPS
{
	FVertexFactoryInterpolantsVSToPS FactoryInterpolants;
	float4 PixelPosition : TEXCOORD6;
	float4 CameraVectorOrVertexColor : TEXCOORD7;
	float4 Position : POSITION;
};

#if USING_SM5_TESSELATION	
	struct FHitProxyVSToDS
	{
		FVertexFactoryInterpolantsVSToDS FactoryInterpolants;
		
		#if PER_PIXEL_CAMERA_VECTOR
			float4 VertexColor : TEXCOORD7;
		#endif

		float4 Position : VS_To_DS_Position;
	};
	
	#define FHitProxyVSOutput FHitProxyVSToDS
#else
	#define FHitProxyVSOutput FHitProxyVSToPS
#endif

#if USING_SM5_TESSELATION	
	#define FPassSpecificVSToDS FHitProxyVSToDS
	#define FPassSpecificVSToPS FHitProxyVSToPS
	
	// Hooks for tessellators to retrieve attributes from vertices
	float4 PassGetWorldPosition(FHitProxyVSToDS Vtx)
	{
		return Vtx.Position;
	}
	
	float4 PassGetWorldNormal(FHitProxyVSToDS Vtx)
	{
		return VertexFactoryGetWorldNormal(Vtx.FactoryInterpolants);
	}
	
	FHitProxyVSToDS PassInterpolate(FHitProxyVSToDS a, float aInterp, FHitProxyVSToDS b, float bInterp)
	{
		FHitProxyVSToDS O;

		O.FactoryInterpolants = VertexFactoryInterpolate(a.FactoryInterpolants, aInterp, b.FactoryInterpolants, bInterp);
		
		#if PER_PIXEL_CAMERA_VECTOR
			TESSELLATION_INTERPOLATE_MEMBER(VertexColor);
		#endif

		return O;
	}

	// X = edge, Y = Inside
	float2 PassGetTessellationFactors(FHitProxyVSToDS I)
	{
		FMaterialTessellationParameters MaterialParameters = GetMaterialTessellationParameters(I.FactoryInterpolants, PassGetWorldPosition(I).xyz);
		return GetMaterialTessellationFactors(MaterialParameters);
	}

	FHitProxyVSToPS PassFinalizeTessellationOutput(FHitProxyVSToDS Interpolants, float4 WorldPosition)
	{
		FHitProxyVSToPS O;
		
		O.FactoryInterpolants = VertexFactoryAssignInterpolants(Interpolants.FactoryInterpolants);
		
		// Calc displacement mapping
		FMaterialTessellationParameters MaterialParameters = GetMaterialTessellationParameters(Interpolants.FactoryInterpolants, WorldPosition.xyz);
		float3 WorldDisplacement = GetMaterialWorldDisplacement(MaterialParameters);

		WorldPosition.xyz += WorldDisplacement;
		
		// Apply effects of displacement
		#if !PER_PIXEL_CAMERA_VECTOR
			O.CameraVectorOrVertexColor = 1;
			O.CameraVectorOrVertexColor.xyz = TransformWorldVectorToTangent(MaterialParameters.TangentToWorld,CameraPosition_DS.xyz - WorldPosition.xyz * CameraPosition_DS.w);
		#else
			O.CameraVectorOrVertexColor = Interpolants.VertexColor;
		#endif

		// Finally, transform position to clip-space
		O.Position = MulMatrix(ViewProjectionMatrix_DS,WorldPosition);
			
	#if WORLD_POS
		O.PixelPosition = WorldPosition;
	#else
		O.PixelPosition = O.Position;
	#endif
		
		return O;
	}
	
	#include "Tessellation.usf"
#endif

#if VERTEXSHADER

void Main(
	FVertexFactoryInput Input,
	out FHitProxyVSOutput Output
	)
{
	FVertexFactoryIntermediates VFIntermediates = GetVertexFactoryIntermediates(Input);
	float4	WorldPosition = VertexFactoryGetWorldPosition(Input, VFIntermediates);
	float3x3 TangentBasis = VertexFactoryGetTangentBasis(Input, VFIntermediates);

	FMaterialVertexParameters VertexParameters = GetMaterialVertexParameters(Input, VFIntermediates, WorldPosition.xyz, TangentBasis);
	WorldPosition.xyz += GetMaterialWorldPositionOffset(VertexParameters);

#if USING_SM5_TESSELATION
	Output.Position = WorldPosition;
	Output.FactoryInterpolants = VertexFactoryGetInterpolantsVSToDS(Input, VFIntermediates);
	
	#if PER_PIXEL_CAMERA_VECTOR
		Output.VertexColor = VertexFactoryGetVertexColor( Input, VFIntermediates );
	#endif
	
#else

	Output.Position = MulMatrix(ViewProjectionMatrix,WorldPosition);
	Output.FactoryInterpolants = VertexFactoryGetInterpolantsVSToPS(Input, VFIntermediates);
	#if WORLD_POS
		Output.PixelPosition = WorldPosition;
	#else
		Output.PixelPosition = Output.Position;
	#endif

	Output.CameraVectorOrVertexColor = 1;
	#if PER_PIXEL_CAMERA_VECTOR
		Output.CameraVectorOrVertexColor = VertexFactoryGetVertexColor(Input, VFIntermediates);
	#else
		Output.CameraVectorOrVertexColor.xyz = VertexFactoryWorldToTangentSpace(Input,VFIntermediates,TangentBasis,CameraPosition.xyz - WorldPosition.xyz * CameraPosition.w);
	#endif
	
#endif
}

#endif // VERTEXSHADER
