/*=============================================================================
	SpotLightVertexShader.hlsl: Spot light vertex shader.
	Copyright 1998-2011 Epic Games, Inc. All Rights Reserved.
=============================================================================*/

#define NEEDS_LIGHTMAP_COORDINATE STATICLIGHTING_TEXTUREMASK

#include "Common.usf"
#include "Material.usf"
#include "VertexFactory.usf"

float4 LightPositionAndInvRadius; // w = 1.0 / Radius

struct FSpotLightVSToPS
{
	FVertexFactoryInterpolantsVSToPS FactoryInterpolants;
	
	float3 TangentLightVector : TEXCOORD4;
	float4 WorldLightVector : TEXCOORD5;
	float4 CameraVectorOrVertexColor : TEXCOORD6;
	float4 PixelPosition : TEXCOORD7;
	float4 Position : POSITION;
};

#if USING_SM5_TESSELATION	
	struct FSpotLightVSToDS
	{
		FVertexFactoryInterpolantsVSToDS FactoryInterpolants;
		
		float3 TangentLightVector : TEXCOORD4;
		float4 WorldLightVector : TEXCOORD5;

		#if PER_PIXEL_CAMERA_VECTOR
			float4 VertexColor : TEXCOORD6;
		#endif

		float4 Position : VS_To_DS_Position;
	};
	
	#define FSpotLightVSOutput FSpotLightVSToDS
#else
	#define FSpotLightVSOutput FSpotLightVSToPS
#endif

#if USING_SM5_TESSELATION	
	#define FPassSpecificVSToDS FSpotLightVSToDS
	#define FPassSpecificVSToPS FSpotLightVSToPS
	
	// Hooks for tessellators to retrieve attributes from vertices
	float4 PassGetWorldPosition(FSpotLightVSToDS Vtx)
	{
		return Vtx.Position;
	}
	
	float4 PassGetWorldNormal(FSpotLightVSToDS Vtx)
	{
		return VertexFactoryGetWorldNormal(Vtx.FactoryInterpolants);
	}

	FSpotLightVSToDS PassInterpolate(FSpotLightVSToDS a, float aInterp, FSpotLightVSToDS b, float bInterp)
	{
		FSpotLightVSToDS O;
		
		O.FactoryInterpolants = VertexFactoryInterpolate(a.FactoryInterpolants, aInterp, b.FactoryInterpolants, bInterp);
		
		#if PER_PIXEL_CAMERA_VECTOR
			TESSELLATION_INTERPOLATE_MEMBER(VertexColor);
		#endif
		
		TESSELLATION_INTERPOLATE_MEMBER(TangentLightVector);
		TESSELLATION_INTERPOLATE_MEMBER(WorldLightVector);

		return O;
	}

	// X = edge, Y = Inside
	float2 PassGetTessellationFactors(FPassSpecificVSToDS I)
	{
		FMaterialTessellationParameters MaterialParameters = GetMaterialTessellationParameters(I.FactoryInterpolants, PassGetWorldPosition(I).xyz);
		return GetMaterialTessellationFactors(MaterialParameters);
	}

	FSpotLightVSToPS PassFinalizeTessellationOutput(FSpotLightVSToDS Interpolants, float4 WorldPosition)
	{
		FSpotLightVSToPS O;
		
		O.FactoryInterpolants = VertexFactoryAssignInterpolants(Interpolants.FactoryInterpolants);
		
		// Calc displacement mapping
		FMaterialTessellationParameters MaterialParameters = GetMaterialTessellationParameters(Interpolants.FactoryInterpolants, WorldPosition.xyz);
		float3 WorldDisplacement = GetMaterialWorldDisplacement(MaterialParameters);

		WorldPosition.xyz += WorldDisplacement;
		
		// Apply effects of displacement
		#if !PER_PIXEL_CAMERA_VECTOR
			O.CameraVectorOrVertexColor = 1;
			O.CameraVectorOrVertexColor.xyz = TransformWorldVectorToTangent(MaterialParameters.TangentToWorld,CameraPosition_DS.xyz - WorldPosition.xyz * CameraPosition_DS.w);
		#else
			O.CameraVectorOrVertexColor = Interpolants.VertexColor;
		#endif

		O.TangentLightVector = Interpolants.TangentLightVector;
		O.WorldLightVector = Interpolants.WorldLightVector;

		// Finally, transform position to clip-space
		O.Position = MulMatrix(ViewProjectionMatrix_DS,WorldPosition);
			
	#if WORLD_POS
		O.PixelPosition = WorldPosition;
	#else
		O.PixelPosition = O.Position;
	#endif
		
		return O;
	}
	
	#include "Tessellation.usf"
#endif

#if VERTEXSHADER

void Main(
	FVertexFactoryInput Input,
#if STATICLIGHTING_VERTEXMASK
	float LightMask : BLENDWEIGHT0,
#endif
	out FSpotLightVSOutput Output
	)
{
	FVertexFactoryIntermediates VFIntermediates = GetVertexFactoryIntermediates(Input);
	float4 WorldPosition = VertexFactoryGetWorldPosition(Input, VFIntermediates);
	float3x3 TangentBasis = VertexFactoryGetTangentBasis(Input, VFIntermediates);

	FMaterialVertexParameters VertexParameters = GetMaterialVertexParameters(Input, VFIntermediates, WorldPosition.xyz, TangentBasis);
	// Isolate instructions used for world position offset on xbox 360, 
	// As these cause the optimizer to generate different position calculating instructions in each pass, resulting in self-z-fighting.
	// This is only necessary for shaders used in passes that have depth testing enabled.
	ISOLATE
	{
		WorldPosition.xyz += GetMaterialWorldPositionOffset(VertexParameters);
	}

	// Calculating light vectors off of untessellated position, the difference is rarely noticeable
	Output.TangentLightVector = VertexFactoryWorldToTangentSpace(Input,VFIntermediates,TangentBasis,LightPositionAndInvRadius.xyz - WorldPosition.xyz);
	Output.WorldLightVector = float4((LightPositionAndInvRadius.xyz - WorldPosition.xyz) * LightPositionAndInvRadius.w,0);
	#if STATICLIGHTING_VERTEXMASK
		Output.WorldLightVector.w = LightMask;
	#endif

#if USING_SM5_TESSELATION
	Output.Position = WorldPosition;
	Output.FactoryInterpolants = VertexFactoryGetInterpolantsVSToDS(Input, VFIntermediates);
	
	#if PER_PIXEL_CAMERA_VECTOR
		Output.VertexColor = VertexFactoryGetVertexColor( Input, VFIntermediates );
	#endif
	
#else

	Output.Position = MulMatrix(ViewProjectionMatrix,WorldPosition);
	Output.FactoryInterpolants = VertexFactoryGetInterpolantsVSToPS(Input, VFIntermediates);
	#if WORLD_POS
		Output.PixelPosition = WorldPosition;
	#else
		Output.PixelPosition = Output.Position;
	#endif

	Output.CameraVectorOrVertexColor = 1;
	#if PER_PIXEL_CAMERA_VECTOR
		Output.CameraVectorOrVertexColor = VertexFactoryGetVertexColor( Input, VFIntermediates );
	#else
		Output.CameraVectorOrVertexColor.xyz = VertexFactoryWorldToTangentSpace(Input,VFIntermediates,TangentBasis,CameraPosition.xyz - WorldPosition.xyz * CameraPosition.w);
	#endif
	
#endif
}

#endif // VERTEXSHADER
